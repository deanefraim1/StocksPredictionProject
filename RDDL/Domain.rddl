domain STOCK_model {
    requirements = {
        constrained-state
    };

    types {
        stock : object;
        time : object;
    };

    pvariables {
        ///////////////////////////////////////////
        // non-fluents == topology and geometry
        ///////////////////////////////////////////

        // stock price
        STOCK-PRICE(stock, time)                                                :  { non-fluent, real, default = 0.0 };

        // Buy Commission Rate
        BUY-COMMISSION-RATE                                                     :  { non-fluent, real, default = 0.0 };

        // Sell Commission Rate
        SELL-COMMISSION-RATE                                                    :  { non-fluent, real, default = 0.0 };

        // Next time dynamics
        NEXT(time, time)                                                        :  { non-fluent, bool, default = false };


        //////////////////////////////
        // interm fluents
        //////////////////////////////

        // Toal transfer Commission
        TotalCommission(stock, stock)                                           :  { interm-fluent, real };

        // Money from stock transfer
        MoneyTransfer(stock, stock)                                             :  { interm-fluent, real };

        // CurrentTimeStockPrice
        CurrentTimeStockPrice(stock)                                            :  { interm-fluent, real };


        //////////////////////////////
        // states fluents
        //////////////////////////////

        // current time
        CurrentTime(time)                                                       :  { state-fluent, bool, default = false};

        // number of shares owned
        Shares(stock)                                                           :  { state-fluent, real, default = 0.0};

        //////////////////////////////
        // action fluents
        //////////////////////////////

        // amount of shares to transfer
        Transfer(stock, stock)                                                  :  { action-fluent, real, default = 0.0};


        //////////////////////////////
        // observation fluents
        //////////////////////////////
    };

    cpfs {
        ////////////////////////////////////
        // interm fluents
        ////////////////////////////////////

        // Total transfer Commission
        TotalCommission(?sellStock, ?buyStock) = MoneyTransfer(?sellStock, ?buyStock) * (BUY-COMMISSION-RATE + SELL-COMMISSION-RATE);

        // The money from stock transfer 
        MoneyTransfer(?sellStock, ?buyStock) = Transfer(?sellStock, ?buyStock) * CurrentTimeStockPrice(?sellStock);


        // CurrentTimeStockPrice
        CurrentTimeStockPrice(?s) = (sum_{?t : time} [ CurrentTime(?t) * STOCK-PRICE(?s, ?t) ]);

        ////////////////////////////////////
        // State space evolution
        ////////////////////////////////////

        // Time evolution
        CurrentTime'(?t2) = (sum_{?t1 : time} [ CurrentTime(?t1) * NEXT(?t1, ?t2) ]) > 0;

        // Number of shares owned dynamic
        Shares'(?currStock) = Shares(?currStock) + sum_{?otherStock : stock} [ ((MoneyTransfer(?otherStock, ?currStock) - TotalCommission(?otherStock, ?currStock))/CurrentTimeStockPrice(?currStock))
                                                                               - ((MoneyTransfer(?currStock, ?otherStock) - TotalCommission(?currStock, ?otherStock))/CurrentTimeStockPrice(?otherStock)) 
                                                                             ];
                                          
    };

    reward = (sum_{?s : stock} [ Shares'(?s) * (sum_{?t : time} [ CurrentTime'(?t) * STOCK-PRICE(?s, ?t) ]) ])
             - (sum_{?s : stock} [ Shares(?s) * (sum_{?t : time} [ CurrentTime(?t) * STOCK-PRICE(?s, ?t) ]) ]); 

    action-preconditions {
        forall_{?s1 : stock} [ (sum_{?s2 : stock} [ Transfer(?s1, ?s2) ]) - Shares(?s1) <= 0.0 ]; // This condition is to ensure that we are not transferring more shares than we have
        forall_{?s1 : stock, ?s2 : stock} [ Transfer(?s1, ?s2) >= 0.0 ]; // This condition is to ensure that we are not transferring negative shares
        (sum_{?s : stock} [ Transfer(?s, ?s) ]) >= 0.0; // This condition and the next one are to ensure that we are not transferring from one stock to the same stock
        (sum_{?s : stock} [ Transfer(?s, ?s) ]) <=  0.0;
    };

    state-invariants {
        forall_{?s : stock} [ Shares(?s) >= 0.0 ]; // This condition is to ensure that we are not owning negative shares
	};
}