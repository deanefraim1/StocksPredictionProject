domain STOCK_model {
    requirements = {
        constrained-state
    };

    types {
        stock : object;
        time : int{1..10000};
    };

    pvariables {
        ///////////////////////////////////////////
        // non-fluents == topology and geometry
        ///////////////////////////////////////////

        // stock price
        STOCK-PRICE(stock, time)                                                :  { non-fluent, real, default = 0 };

        // Buy Commission Rate
        BUY-COMMISSION-RATE                                                     :  { non-fluent, real, default = 0 };

        // Sell Commission Rate
        SELL-COMMISSION-RATE                                                    :  { non-fluent, real, default = 0 };

        //TERMINAL-TIME(time)                                                      :  { non-fluent, bool, default = false };


        //////////////////////////////
        // interm fluents
        //////////////////////////////

        // Buy Commission
        BuyCommission(stock, stock)                                                    :  { interm-fluent, real };

        // Sell Commission
        SellCommission(stock, stock)                                                   :  { interm-fluent, real };

        // Toal transfer Commission
        TotalCommission(stock, stock)                                                  :  { interm-fluent, real };

        // Money from stock transfer
        MoneyTransfer(stock, stock)                                             :  { interm-fluent, real };


        //////////////////////////////
        // states fluents
        //////////////////////////////

        // current time
        CurrentTime                                                             :  { state-fluent, int, default = 1};

        // number of shares owned
        Shares(stock)                                                           :  { state-fluent, real, default = 0};


        //////////////////////////////
        // action fluents
        //////////////////////////////

        // amount of shares to transfer
        Transfer(stock, stock)                                                  :  { action-fluent, real, default = 0};


        //////////////////////////////
        // observation fluents
        //////////////////////////////
    };

    cpfs {
        ////////////////////////////////////
        // interm fluents
        ////////////////////////////////////

        // Buy Commission
        BuyCommission(?s1, ?s2) = Transfer(?s1, ?s2) * STOCK-PRICE(?s1, CurrentTime) * BUY-COMMISSION-RATE;

        // Sell Commission
        SellCommission(?s1, ?s2) = Transfer(?s1, ?s2) * STOCK-PRICE(?s1, CurrentTime) * SELL-COMMISSION-RATE;

        // Total transfer Commission
        TotalCommission(?s1, ?s2) = BuyCommission(?s1, ?s2) + SellCommission(?s1, ?s2);

        // Money from stock transfer
        MoneyTransfer(?s1, ?s2) = Transfer(?s1, ?s2) * STOCK-PRICE(?s1, CurrentTime);
        

        ////////////////////////////////////
        // State space evolution
        ////////////////////////////////////

        // Time evolution
        CurrentTime' = CurrentTime + 1;

        // Number of shares owned dynamic
        Shares'(?stock) = Shares(?stock) + sum_{?s : stock} [ ((MoneyTransfer(?s, ?stock) - TotalCommission(?s, ?stock))/STOCK-PRICE(?stock, CurrentTime))
                                                            - ((MoneyTransfer(?stock, ?s) - TotalCommission(?s, ?stock))/STOCK-PRICE(?s, CurrentTime)) ];
    };

    reward = sum_{?s : stock} [ Shares'(?s) * (STOCK-PRICE(?s, CurrentTime') - STOCK-PRICE(?s, CurrentTime)) ];

    action-preconditions {
		forall_{?s1 : stock, ?s2 : stock} [ Transfer(?s1, ?s2) <= Shares(?s1) ];
	};

    state-invariants {
        forall_{?s : stock} [ Shares(?s) >= 0 ];
        //forall_{?s : stock, ?t : int} [ STOCK-PRICE(?s, ?t) >= 0 ];
	};
}